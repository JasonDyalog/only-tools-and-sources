#!/usr/bin/env coffee
# A libreadline-based REPL wrapper around Dyalog's interpreter.
# Requires NodeJS and CoffeeScript.

completions = '''
  ⎕á ⎕a ⎕ai ⎕an ⎕arbin ⎕arbout ⎕at ⎕av ⎕avu ⎕base ⎕class ⎕clear ⎕cmd ⎕cr ⎕cs
  ⎕ct ⎕cy ⎕d ⎕dct ⎕df ⎕div ⎕dl ⎕dm ⎕dmx ⎕dq ⎕dr ⎕ed ⎕em ⎕en ⎕ex ⎕exception
  ⎕export ⎕fappend ⎕favail ⎕fchk ⎕fcopy ⎕fcreate ⎕fdrop ⎕ferase ⎕fhold ⎕fix
  ⎕flib ⎕fmt ⎕fnames ⎕fnums ⎕fprops ⎕fr ⎕frdac ⎕frdci ⎕fread ⎕frename ⎕freplace
  ⎕fresize ⎕fsize ⎕fstac ⎕fstie ⎕ftie ⎕funtie ⎕fx ⎕instances ⎕io ⎕kl ⎕lc ⎕load
  ⎕lock ⎕lx ⎕map ⎕ml ⎕monitor ⎕na ⎕nappend ⎕nc ⎕ncreate ⎕nerase ⎕new ⎕nl ⎕nlock
  ⎕nnames ⎕nnums ⎕nq ⎕nr ⎕nread ⎕nrename ⎕nreplace ⎕nresize ⎕ns ⎕nsi ⎕nsize
  ⎕ntie ⎕null ⎕nuntie ⎕nxlate ⎕off ⎕or ⎕opt ⎕path ⎕pfkey ⎕pp ⎕profile ⎕pw ⎕refs
  ⎕r ⎕rl ⎕rtl ⎕s ⎕save ⎕sd ⎕se ⎕sh ⎕shadow ⎕si ⎕signal ⎕size ⎕sm ⎕sr ⎕src
  ⎕stack ⎕state ⎕stop ⎕svc ⎕svo ⎕svq ⎕svr ⎕svs ⎕tc ⎕tcnums ⎕tget ⎕this ⎕tid
  ⎕tkill ⎕tname ⎕tnums ⎕tpool ⎕tput ⎕treq ⎕trace ⎕trap ⎕ts ⎕tsync ⎕ucs ⎕using
  ⎕vfi ⎕vr ⎕wa ⎕wc ⎕wg ⎕wn ⎕ws ⎕wsid ⎕wx ⎕xml ⎕xsi ⎕xt )classes )clear
  )cmd )continue )copy )cs )drop )ed )erase )events )fns )holds )lib )load
  )methods )ns )objects )obs )off )ops )pcopy )props )reset )save )sh )si )sic
  )sinl )tid )vars )wsid )xload
'''.split /[ \n]+/

p = require('child_process').spawn 'apl', ['-q', '-script']
bell = 7 # the Unicode code point that tells us when to show the 6-space prompt
p.stdin.write "⎕io⎕ct←0⋄⎕pw←32767⋄{}8835⌶2 #{bell}\n"
[['stdout', 33], ['stderr', 31]].forEach ([stream, colour]) ->
  p[stream].on 'data', (data) ->
    process['stdout'].write "\x1b[01;#{colour}m#{data}\x1b[00m"
    if data[data.length - 1] is bell then rl.prompt()
  p[stream].on 'close', -> process.exit 0
rl = require('readline').createInterface
  input: process.stdin
  output: process.stdout
  completer: (line) -> w = line.replace /^.*([⎕\)]\w*)$/, '$1'; [completions.filter((c) -> !c.indexOf w), w]
rl.setPrompt '      '
rl.on 'line', (line) -> p.stdin.write line + '\n'
rl.on 'close', -> process.stdout.write '\r'; process.exit 0
rl.prompt()
